#!/bin/zsh

typeset url='http://localhost:8983/solr/blacklight-core/update?commit=true'

(( $# > 0 )) || set -- inbox/*.json*(N)

main() {
    (( $# > 0 )) || fatal "No files to import"
    for f in $@; do
        case $f in
            (*.json.gz)
                print "Processing $f ..." >&2
                gunzip < $f | curl $url -H 'Content-type:application/json' --data-binary @-
                ;;
            (*.json)
                print "Processing $f ..." >&2
                curl $url -H 'Content-type:application/json' --data-binary @$f
                ;;
            (*) fatal "Unrecognized file type: $f"
                ;;
        esac
        mv $f inbox/archive/
    done
}

usage() {
    print 'usage: solrjson FILE...' >&2
    exit 1
}

fatal() {
    print solrjson: "$@" >&2
    exit 2
}

main "$@"
